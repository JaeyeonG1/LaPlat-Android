// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"

        // Androidx Navigation
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.4.0-beta02"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.18.1"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.0"
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    detekt {
        toolVersion = "1.18.1"
        config = files("../detekt-config.yml")
    }
}

task copyGitHooks(type: Copy) {
    from("/teamConfig/git/git-hooks/") {
        include '**/*'
        rename '(.*)', '$1'
    }
    into "/.git/hooks"
}

task installGitHooks(type: Exec) {
    group 'git hooks'
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
